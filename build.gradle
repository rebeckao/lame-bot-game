plugins {
    id 'java'
    id 'co.riiid.gradle' version '0.4.2'
}

version '1.0.epsilon'

// Don't forget to update the version above when releasing!
// To build a jar file and release it on Github, in the terminal run:
// >>gradlew release
// Note: A valid Github token for this repository must be set in the System environment variable GITHUB_TOKEN
// To access the released jar in another project, add this to the dependencies in build.jar:
// compile 'com.github.rebeckao:lame-bot-game:v<the released version>'
task release(type: GradleBuild) {
    tasks = ['fatJar', 'githubRelease']
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File',
                'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.compile.collect {
        it.isDirectory() ? it : zipTree(it)
    } }
    with jar
}

github {
    owner = 'rebeckao'
    repo = 'lame-bot-game'
    token = System.getenv('GITHUB_TOKEN')
    tagName = 'v' + version
    assets = [
            'build/libs/' + project.name + '-' + version + '.jar'
    ]
}
